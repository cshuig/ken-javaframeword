<?xml version="1.0" encoding="utf-8"?>
<view:BaseSimpleContainer xmlns:fx="http://ns.adobe.com/mxml/2009" 
						  xmlns:s="library://ns.adobe.com/flex/spark" 
						  xmlns:mx="library://ns.adobe.com/flex/mx" 
						  xmlns:view="com.shine.framework.core.view.*" 
						  initialize="init();">
	<view:layout>
		<s:BasicLayout/>
	</view:layout>
	<fx:Declarations>
		<!-- 将非可视元素（例如服务、值对象）放在此处 -->
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
			import mx.controls.Alert; 
			import mx.events.SliderEvent; 
			import mx.events.VideoEvent; 
			
			//[Embed(source="assets/videoIco/play_small.png")] 
			[Bindable] 
			private var playClass:Class;                //播放图标样式 
			
			//[Embed(source="assets/videoIco/pause.png")] 
			[Bindable] 
			private var pauseClass:Class;               //暂停图标样式 
			
			//[Embed(source="assets/videoIco/sound1.png")] 
			[Bindable] 
			private var sound1:Class;               //声音样式1 
			
			//[Embed(source="assets/videoIco/sound.png")] 
			[Bindable] 
			private var sound:Class;                //声音样式2（静音） 
			
			[Bindable] 
			public var videoSource:String;         //媒体路径 
			
			private var isPause:Boolean = false;        //暂停状态 
			private var isSound:Boolean = true;         //声音状态 
			private var isFullScreen:Boolean = false;   //是否是全屏 
			private var tmpSound:Number = 0;            //临时声音大小 
			[Bindable] 
			private var playPosition:Number;            //播放進度  
			
			private function init():void{ 
				//flv 地址
				videoSource = ""; 
			} 
			
			private function playingMove(event:VideoEvent):void{ 
				my_hs.value = myVid.playheadTime; 
			} 
			
			//进度条改变 
			private function hs_onchange(event:SliderEvent):void{ 
				if(myVid.playheadTime == -1){ 
					my_hs.value = 0; 
					return; 
				} 
				playPosition = my_hs.value;             //保正播放进度統一 
				myVid.playheadTime = playPosition; 
			} 
			
			//进度条鼠标按下 
			private function thumbPress():void{ 
				myVid.pause(); 
			} 
			//进度条鼠标弹起 
			private function thumbRelease():void{ 
				myVid.playheadTime = playPosition; 
				myVid.play(); 
			} 
			
			
			//播放，暂停 
			private function playButton():void{ 
				if(!isPause){ 
					myVid.play(); 
					playBtn.source = pauseClass; 
					isPause = true; 
				}else{ 
					myVid.pause(); 
					playBtn.source = playClass; 
					isPause = false; 
				} 
			} 
			
			//播放完成 
			private function vidComplete():void{ 
				playBtn.source = playClass; 
				isPause = false; 
			} 
			
			//停止播放 
			private function stopButton():void{ 
				myVid.stop(); 
				playBtn.source = playClass; 
				isPause = false; 
			} 
			
			//切換全屏顯示 
			private function display():void{ 
				if(!isFullScreen){ 
					stage.fullScreenSourceRect =new Rectangle(myVid.x,myVid.y,myVid.width,myVid.height);              
					stage.displayState =StageDisplayState.FULL_SCREEN; 
					isFullScreen = true; 
				}else{ 
					stage.displayState = StageDisplayState.NORMAL; 
					isFullScreen = false; 
				} 
			} 
			
			//调整声音 
			private function sound_thumbChanges(event:SliderEvent):void{ 
				myVid.volume = hs_sound.value; 
			} 
			
			//静音 
			private function closeSound():void{ 
				if(isSound){ 
					closeImg.source = sound; 
					tmpSound = myVid.volume; 
					myVid.volume = 0; 
					isSound = false; 
				}else{ 
					closeImg.source = sound1; 
					myVid.volume = tmpSound; 
					isSound = true; 
				} 
			} 
			
			//格式化时间 
			private function formatTime(time:Number):String{ 
				var min:Number = Math.floor(time/60); 
				var sec:Number = Math.floor(time%60); 
				
				var timeResult:String = (min < 10 ? "0"+min.toString() : min.toString()) + ":" + (sec < 10 ? "0"+sec.toString() : sec.toString()); 
				
				return timeResult; 
			} 
			
			//slider格式化 
			private function dataTipFormat(data:Number):String{ 
				return formatTime(data); 
			} 
		]]>
	</fx:Script>
	<mx:VideoDisplay id="myVid" x="10" y="10" height="360" width="480"   
					 source="{videoSource}"   
					 autoPlay="false"   
					 playheadUpdate="playingMove(event)"   
					 complete="vidComplete();"   
					 doubleClickEnabled="true"  
					 doubleClick="display();"/>  
	
	<mx:HBox width="473" verticalAlign="middle" x="17" y="395">  
		<mx:Image source="{playClass}" click="playButton();" id="playBtn" buttonMode="true"/>  
		<mx:Image source="@Embed('assets/videoIco/stop.png')" click="stopButton();" buttonMode="true"/>  
		<mx:Label text="{formatTime(myVid.playheadTime)}/{formatTime(myVid.totalTime)}" width="112"/>  
		<mx:HRule height="0" width="80"/>  
		<mx:Image source="{sound1}" click="closeSound();" id="closeImg" buttonMode="true"/>  
		<mx:HSlider width="96" id="hs_sound" minimum="0" maximum="1"   
					change="sound_thumbChanges(event)"  
					value="{myVid.volume}"/>  
		<mx:Button label="全屏" click="display();"  cornerRadius="20"/>  
	</mx:HBox>  
	<mx:HSlider width="478" id="my_hs" minimum="0" maximum="{myVid.totalTime}" height="9" x="12" y="378"  
				horizontalCenter="0"  
				showTrackHighlight="true"  
				change="hs_onchange(event)"  
				thumbPress="thumbPress();"  
				thumbRelease="thumbRelease();"  
				dataTipFormatFunction="dataTipFormat"/> 
</view:BaseSimpleContainer>
